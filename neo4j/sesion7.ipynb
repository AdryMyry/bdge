{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {
    "collapsed": true
   },
   "source": [
    "# NoSQL (Neo4j) (sesión 7)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "![Image of HBase](https://neo4j.com/wp-content/themes/neo4jweb/assets/images/neo4j-logo-2015.png)\n",
    "\n",
    "Esta hoja muestra cómo acceder a bases de datos Neo4j y también a conectar la salida con Jupyter.\n",
    "\n",
    "Se puede utilizar el propio interfaz de Neo4j también en la dirección http://127.0.0.1:7474."
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Iniciamos Neo4j:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "collapsed": false,
    "scrolled": true
   },
   "outputs": [],
   "source": [
    "%%bash\n",
    "~/start-neo4j.sh"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "collapsed": false
   },
   "outputs": [],
   "source": [
    "from pprint import pprint as pp\n",
    "import pandas as pd\n",
    "import matplotlib.pyplot as plt\n",
    "import matplotlib\n",
    "\n",
    "%matplotlib inline\n",
    "matplotlib.style.use('ggplot')"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Vamos a cargar la extensión `ipython-cypher` para poder lanzar consultas Cypher directamente a través de la hoja. En mi ordenador he configurado \"`root`\" como el _password_.\n",
    "\n",
    "Para iniciar Neo4j hay que ir primero a la página de su browser y hacer login con el usuario y clave iniciales:\n",
    "\n",
    "http://127.0.0.1:7474\n",
    "\n",
    "Usuario: neo4j\n",
    "Password: neo4j\n",
    "\n",
    "Después pide la nueva clave. Yo he puesto \"`root`\" en esta hoja.\n",
    "\n",
    "Utilizaremos una extensión de Jupyter Notebook que se llama `ipython-cypher`. Está instalada en la máquina virtual. Si no, se podría instalar con:\n",
    "\n",
    "    sudo pip2 install ipython-cypher\n",
    "    \n",
    "Después, todas las celdas que comiencen por `%%cypher` y todas las instrucciones Python que comiencen por `%cypher` se enviarán a Neo4j para su interpretación"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "collapsed": false
   },
   "outputs": [],
   "source": [
    "%load_ext cypher\n",
    "%config CypherMagic.uri='http://neo4j:root@127.0.0.1:7474/db/data'"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "collapsed": false
   },
   "outputs": [],
   "source": [
    "%config CypherMagic.auto_html=True"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "La carga de datos CSV no se puede realizar directamente desde la hoja, porque la extensión `ipython-cypher` falla por alguna cuestión de codificación UTF-8. Además, el CSV que acepta Neo4j no es estándar y he enviado un fallo para que lo arreglen (no parece que estén muy por la labor):\n",
    "\n",
    "https://github.com/neo4j/neo4j/issues/8472\n",
    "\n",
    "Mientras tanto, se pueden importar los Posts de Stackoverflow español simplemente añadiendo una barra inversa a cada barra inversa que aparece en el fichero:\n",
    "\n",
    "    sed -ie 's/\\\\/\\\\\\\\/g' Posts.csv\n",
    "    \n",
    "Ya lo he preparado en el fichero que aparece abajo. Sin embargo, no se puede ejecutar aquí, hay que ir al browser: http://127.0.0.1:7474."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "collapsed": false,
    "scrolled": true
   },
   "outputs": [],
   "source": [
    "%%cypher\n",
    "USING PERIODIC COMMIT 10000\n",
    "LOAD CSV WITH HEADERS FROM \"http://neuromancer.inf.um.es:8080/es.stackoverflow/Posts-for-neo4j.csv\" AS row\n",
    "CREATE (n)\n",
    "SET n = row\n",
    "FOREACH(ignoreMe IN CASE WHEN trim(row.PostTypeId) = \"1\" THEN [1] ELSE [] END | \n",
    "  SET n :Question)\n",
    "FOREACH(ignoreMe IN CASE WHEN trim(row.PostTypeId) = \"2\" THEN [1] ELSE [] END | \n",
    "  SET n :Answer)\n",
    "FOREACH(ignoreMe IN CASE WHEN trim(row.OwnerUserId) <> \"\" THEN [1] ELSE [] END | \n",
    "  MERGE (u:User {Id: n.OwnerUserId})\n",
    "  MERGE (u)-[:WROTE]->(n)\n",
    ")\n",
    "SET n :Post\n",
    ";"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### El lenguaje Cypher\n",
    "\n",
    "El lenguaje Cypher tiene una sintaxis de _Query By Example_. Acepta funciones y permite creación y búsqueda de nodos y relaciones. Tiene algunas peculiaridades que veremos a continuación. Por lo pronto, se puede ver un resumen de características en la [Cypher Reference Card](https://neo4j.com/docs/cypher-refcard/current/).\n",
    "\n",
    "La anterior consulta utiliza la construcción `LOAD CSV` para leer datos CSV dentro de nodos. La cláusula `CREATE` crea nuevos nodos. La `SET` permite poner valores a las propiedades de los nodos.\n",
    "\n",
    "Para hacer asignaciones condicionales, el lenguaje no ofrece una construcción adecuada. Por lo tanto, hay que utilizar una construcción `FOREACH` donde se utiliza otra construcción `CASE WHEN` con una condición. Si se cumple, se devuelve una lista con un elemento. Si no, una lista vacía. En el caso de la lista con un elemento (cuando se cumple la condición) se ejecuta la parte final del `FOREACH` tan sólo una vez.\n",
    "\n",
    "En el caso de la consulta de arriba, a todos los datos leídos se les copia los datos de la línea (primer `SET`). Después, dependiendo del valor de `PostTypeId`, se les etiqueta como `:Question` o como `:Answer`. Si tienen un usuario asignado a través de `OwnerUserId`, se añade un usuario si no existe y se crea la relación `:WROTE`.\n",
    "\n",
    "Finalmente se le asigna también el tag `:Post` a cada pregunta o respuesta. Esto añade más de una etiqueta a algunos nodos, y permite buscarlos por diferentes criterios.\n",
    "\n",
    "También hay otros posts especiales que no eran preguntas ni respuestas. A estos no se les asigna una segunda etiqueta:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "collapsed": true
   },
   "outputs": [],
   "source": [
    "%%cypher\n",
    "match (n:Post) WHERE size(labels(n)) = 1 RETURN n;"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Creamos un índice sobre el `Id` para acelerar las siguientes búsquedas:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "collapsed": false
   },
   "outputs": [],
   "source": [
    "%%cypher\n",
    "CREATE INDEX ON :Post(Id);"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Añadimos una relación entre las preguntas y las respuestas:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "collapsed": false,
    "scrolled": false
   },
   "outputs": [],
   "source": [
    "%%cypher\n",
    "MATCH (a:Answer), (q:Question {Id: a.ParentId})\n",
    "MERGE (a)-[:ANSWERS]->(q)\n",
    ";"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "collapsed": false,
    "scrolled": true
   },
   "outputs": [],
   "source": [
    "%%cypher\n",
    "MATCH q=(r)-[:ANSWERS]->(p) RETURN p,r LIMIT 100;"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "La consulta RQ4 se puede resolver de manera muy fácil. En esta primera consulta se devuelve los nodos:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "collapsed": false
   },
   "outputs": [],
   "source": [
    "%%cypher\n",
    "// RQ4\n",
    "MATCH\n",
    "(u)-[:WROTE]->()-[:ANSWERS]->()<-[:WROTE]-(u2),\n",
    "(u2)-[:WROTE]->()-[:ANSWERS]->()<-[:WROTE]-(u)\n",
    "WHERE u2 <> u AND u.Id < u2.Id\n",
    "RETURN DISTINCT u,u2\n",
    ";"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "O bien retornar los `Id` de cada usuario:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "collapsed": false,
    "scrolled": true
   },
   "outputs": [],
   "source": [
    "%%cypher\n",
    "MATCH\n",
    "(u)-[:WROTE]->()-[:ANSWERS]->()<-[:WROTE]-(u2),\n",
    "(u2)-[:WROTE]->()-[:ANSWERS]->()<-[:WROTE]-(u)\n",
    "WHERE u2 <> u AND toInt(u.Id) < toInt(u2.Id)\n",
    "RETURN DISTINCT u.Id,u2.Id\n",
    "ORDER BY toInt(u.Id)\n",
    ";"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Y finalmente, la creación de relaciones `:RECIPROCATE` entre los usuarios. Se introduce también la construcción `WITH`.\n",
    "\n",
    "`WITH` sirve para introducir \"espacios de nombres\". Permite importar nombres de filas anteriores, hacer alias con `AS` e introducir nuevos valores con funciones de Cypher. La siguiente consulta es la misma de arriba, RQ4, pero creando relaciones `:RECIPROCATE` entre cada dos usuarios que se ayudan recíprocamente."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "collapsed": false
   },
   "outputs": [],
   "source": [
    "%%cypher\n",
    "// RQ4 creando relaciones de reciprocidad\n",
    "MATCH\n",
    "(u:User)-[:WROTE]->()-[:ANSWERS]->()<-[:WROTE]-(u2:User),\n",
    "(u2)-[:WROTE]->()-[:ANSWERS]->()<-[:WROTE]-(u)\n",
    "WHERE u2 <> u AND u.Id < u2.Id\n",
    "WITH u AS user1,u2 AS user2\n",
    "MERGE (user1)-[:RECIPROCATE]->(user2)\n",
    "MERGE (user2)-[:RECIPROCATE]->(user1)\n",
    ";"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## EJERCICIO: Construir los nodos `:Tag` para cada uno de los tags que aparecen en las preguntas. Construir las relaciones `post-[:TAGGED]->tag` para cada tag y también `tag-[:TAGS]->post`\n",
    "\n",
    "Para ello, buscar en la ayuda las construcciones `WITH` y `UNWIND` y las funciones `replace()` y `split()` de Cypher. La siguiente consulta debe retornar 1192 resultados:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "collapsed": false
   },
   "outputs": [],
   "source": [
    "%%cypher\n",
    "MATCH p=(t:Tag)-[:TAGS]->(:Question) WHERE t.name =~ \"^java$|^c\\\\+\\\\+$\" RETURN count(p);"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Python 2",
   "language": "python2",
   "name": "python2"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 2
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython2",
   "version": "2.7.12"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 1
}
